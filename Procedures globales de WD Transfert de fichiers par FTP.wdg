#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : Procedures globales de WD Transfert de fichiers par FTP
 major_version : 28
 minor_version : 0
 type : 7
 description : ""
 subtype : 0
procedure_set :
 identifier : 0x19259d6c035c5a24
 internal_properties : CAAAAAgAAABGLu41kG7fjQV3iS4F72qmnKaNh5694reolNKIW0iw
 code_elements :
  internal_properties : CAAAAAgAAABPOo5wfzL5PtlmH9g6pVlcqG1md+uM4GM8B/k/aVja5R6ZseBzxTGWZbo12UeE8bWym5Rl91AKrNq9wTYnFyamg2lAMw==
  type_code : 31
  p_codes : []
  procedures :
   -
     name : DéconnexionFTP
     procedure_id : 1812027520914908350
     type_code : 15
     code : |1-
      Procédure DéconnexionFTP()
      
      // Les taches doivent s'arrêter
      bFintache = Vrai
      
      OuvreFille(FEN_Attente_Deconnexion)
      
      // Les taches sont finis ?
      i est un entier =1
      
      FinAutomatismeProcédure(MAJGrapheBandePassante)
      FinAutomatismeProcédure(MAJTableDesTransfert)
      FinAutomatismeProcédure(FEN_PRINCIPALE.VérifieConnexionTerminé)
      
      // On supprime les fichiers en attente
      TableauSupprimeTout(COL_FTP_taches_parallèle.gtabTableauListeFichiers)
      
      TANTQUE i <=TableauOccurrence(FEN_PRINCIPALE.gtab_TacheParallèle)
      	SI FEN_PRINCIPALE.gtab_TacheParallèle[i].Etat= tpeAnnulée OU FEN_PRINCIPALE.gtab_TacheParallèle[i].Etat = tpeTerminée ALORS
      		i++
      	SINON
      		// Attentes de la fin des taches parallèles
      		Multitâche(-1)
      	FIN
      FIN
      Ferme(FEN_Attente_Deconnexion)
      
      // On regarde si l'utilisateur est connecté
      FTPDéconnecte(gstConnexionFTP.nIDConnexion)
      gstConnexionFTP.nIDConnexion=-1
      
      // Fin du timer de comptabilisation du temps de connexion
      FinTimer(gstConnexionFTP.nIDConnexion)
      
      // On vide les éléments du FTP
      TableSupprimeTout(FEN_PRINCIPALE.TABLE_Distant)
      TableSupprimeTout(FEN_PRINCIPALE.TABLEH_ArbreDistant)
      TableauSupprimeTout(gtabTailleTransfert)
      
      // On informe l'utilisateur de l'état de la connexion
      Message(<§@19259d6c035b5a140000§>)
     type : 458752
   -
     name : TestFichierHFSQL
     procedure_id : 1331463093250369943
     type_code : 15
     code : |1+
      // Résumé : Teste l'ouverture des fichiers
      // Syntaxe :
      //TestFichierHFSQL ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      
      PROCEDURE TestFichierHFSQL()
      
      // Positionne la vérification à l'identique
      HVérifieStructure("*", hIdentique)
      
      QUAND EXCEPTION DANS
      	// Teste l'ouverture de tous les fichiers
      	HOuvre("*")
      	HFerme("*")
      	TestEcritRésultat(terInfo, "Fichiers OK")
      FAIRE
      	// Soucis en ouverture
      	TestEcritRésultat(terErreur, "Fichiers en erreur : " + ExceptionInfo(errMessage))
      FIN
      
     type : 458752
   -
     name : Reconnexion
     procedure_id : 1616732660674965292
     type_code : 15
     code : |1+
      // Résumé : Reconnecte l'application au ftp
      // Paramètres :
      //	sRepDistant (chaîne ANSI) :Répertoire distant
      // Valeur de retour :
      // 	Aucune
      //
      PROCEDURE Reconnexion(sRepDistant est une chaine)
      FTPDéconnecte(gstConnexionFTP.nIDConnexion)
      
      // Variables locales
      sProtocole est une chaîne
      
      // On recherche le protocole selon la sécurité de la connexion
      SELON gstConfigurationFTP.gnSécuritéConnexion
      	CAS ESecuriteConnexion.NonSecurise
      		// non sécurisée	
      		sProtocole = ""
      		
      	CAS ESecuriteConnexion.SFTP
      		// sécurisée : protocole SFTP
      		sProtocole = "sftp://"
      	CAS ESecuriteConnexion.FTPS
      		// sécurisée : protocole FTPS
      		sProtocole = "ftps://"
      	CAS ESecuriteConnexion.FTPES			
      		// sécurisée : protocole FTPES	
      		sProtocole = "ftpes://"
      		
      FIN
      
      SI gstConfigurationFTP.gbIgnorerErreur = Vrai ALORS
      	FTP.IgnoreErreur = ftpIgnoreNomCertificatInvalide + ftpIgnoreCertificatInvalide + ftpIgnoreCertificatExpiré
      SINON
      	FTP.IgnoreErreur = 0
      FIN
      
      SI gstConfigurationFTP.gbAnonyme=Faux ALORS
      	// Connexion nominative
      	SI gstConfigurationFTP.gbAvance=Faux ALORS
      		// Paramétrage standard
      		gstConnexionFTP.nIDConnexion = FTPConnecte(sProtocole+gstConfigurationFTP.gsAdresse_FTP,gstConfigurationFTP.gsNom_Utilisateur,gstConfigurationFTP.gsMDP_Utilisateur)		
      	SINON
      		// Paramétrage avancé
      		gstConnexionFTP.nIDConnexion = FTPConnecte(sProtocole+gstConfigurationFTP.gsAdresse_FTP,gstConfigurationFTP.gsNom_Utilisateur,...
      		gstConfigurationFTP.gsMDP_Utilisateur,gstConfigurationFTP.gnNumero_port,gstConfigurationFTP.gnTypeConnexion,gstConfigurationFTP.gndelai)
      	FIN  	
      SINON
      	// Connexion anonyme
      	SI gstConfigurationFTP.gbAvance=Faux ALORS
      		// Paramétrage standard
      		gstConnexionFTP.nIDConnexion = FTPConnecte(sProtocole+gstConfigurationFTP.gsAdresse_FTP)
      	SINON
      		// Paramétrage avancé
      		gstConnexionFTP.nIDConnexion = FTPConnecte(sProtocole+gstConfigurationFTP.gsAdresse_FTP,"","",gstConfigurationFTP.gnNumero_port,gstConfigurationFTP.gnTypeConnexion,gstConfigurationFTP.gndelai)
      	FIN  	
      FIN                    
      
      FTPRepEnCours(gstConnexionFTP.nIDConnexion,sRepDistant)
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : CAAAAAgAAACVGQgYbalU7DKC3oH4ItDrjtcTHXXV/EuH8q0IbhyQlw==
  original_name : COL_Collection1
resources :
 string_res :
  identifier : 0x19259d6c035b5a14
  internal_properties : CAAAAAgAAACm76HWfKGWp33VjXInA4cRlqArlgTTA862QGt72W2ld5Y=
  strings :
   -
     text :
      fr-FR : Non connecté
     index : 0
custom_note :
 internal_properties : CAAAAAgAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
associated_test :
 name : TEST_Procedures globales de WD Transfert de fichiers par FTP.wxt
